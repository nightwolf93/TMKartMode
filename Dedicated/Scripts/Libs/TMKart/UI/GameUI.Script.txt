// #RequireContext CSmMlScriptIngame
#Include "Libs/Nadeo/TMGame/Modes/Race.Script.txt" as Race

Void SyncLaps() {
    declare netwrite Integer Net_RaceLapNb for Teams[0];
    Net_RaceLapNb = Race::GetLapsNb();
    if (Net_RaceLapNb < 0) Net_RaceLapNb = 1;
}

Text GetManialink() {
    return """<?xml version="1.0" encoding="utf-8" standalone="yes"?>
    <manialink version="3">
    <frame pos="0 30">    
    <frame pos="115 -55">
    <label id="RankText" pos="20.5 -4.58" z-index="10" size="40 10.5" text="0/32" halign="center" textsize="7" valign="center" style="TextButtonNavBack"/>
    <quad pos="0 0" z-index="0" size="40 10.7" bgcolor="000" opacity="0.7"/>
    </frame>
    <frame pos="115 -42">
    <label id="CurrentTimeText" pos="20.5 -4.58" z-index="10" size="40 10.5" text="00:00.00" halign="center" textsize="4" valign="center" style="TextButtonNav"/>
    <quad pos="0 0" z-index="0" size="40 10.7" bgcolor="000" opacity="0.7"/>
    </frame>
    <frame pos="115 -29">
    <label pos="6.17 -4.91" z-index="10" size="10 10.5" text="ðŸ" halign="center" textsize="4" valign="center" style="TextButtonNav"/>
    <label id="LapText" pos="25.7 -4.58" z-index="10" size="29.7 10.5" text="1/3" halign="center" textsize="4" valign="center" style="TextButtonNav"/>
    <quad pos="0 0" z-index="0" size="40 10.7" bgcolor="000" opacity="0.7"/>
    </frame>
    </frame>
    <script><!--
    #Include "TextLib" as TextLib
    #Include "MathLib" as MathLib

    main()
    {
        declare CMlLabel LapTextLabel = (Page.GetFirstChild("LapText") as CMlLabel);
        declare CMlLabel RankTextLabel = (Page.GetFirstChild("RankText") as CMlLabel);
        declare CMlLabel CurrentTimeTextLabel = (Page.GetFirstChild("CurrentTimeText") as CMlLabel);
        declare netread Integer Net_RaceLapNb for Teams[0];
        while(True)
        {
            yield;
            if(GUIPlayer == Null) continue;

            LapTextLabel.Value = (GUIPlayer.CurrentLapNumber + 1) ^ "/" ^ Net_RaceLapNb;
            RankTextLabel.Value = GUIPlayer.Dossard ^ "/" ^ Players.count;
            if(TextLib::StartsWith("0", GUIPlayer.Dossard)) RankTextLabel.Value = TextLib::SubString(GUIPlayer.Dossard, 1 , 1) ^ "/" ^ Players.count;

            // Update lap time
            CurrentTimeTextLabel.TextColor = TextLib::ToColor("fff");
            CurrentTimeTextLabel.Value = TextLib::TimeToText(GUIPlayer.CurrentLapTime, True, True);
            if(GUIPlayer.CurrentLapTime < 0) CurrentTimeTextLabel.TextColor = TextLib::ToColor("f80");
        }
    }
    --></script>
    </manialink>""";
}